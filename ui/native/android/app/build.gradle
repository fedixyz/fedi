apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"
apply plugin: "kotlin-android"
// needed for push notifications
apply plugin: "com.google.gms.google-services"

/**
 * This is the configuration block to customize your React Native Android app.
 * By default you don't need to apply any configuration, just uncomment the lines you need.
 */

 react {
    /* Folders */
    //   The root of your project, i.e. where "package.json" lives. Default is '..'
    // root = file("../")
    //   The folder where the react-native NPM package is. Default is ../node_modules/react-native
    reactNativeDir = file("../../../node_modules/react-native")
    //   The folder where the react-native Codegen package is. Default is ../node_modules/@react-native/codegen
    codegenDir = file("../../../node_modules/@react-native/codegen")
    //   The cli.js file which is the React Native CLI entrypoint. Default is ../node_modules/react-native/cli.js
    cliFile = file("../../../node_modules/react-native/cli.js")
    /* Variants */
    //   The list of variants to that are debuggable. For those we're going to
    //   skip the bundling of the JS bundle and the assets. By default is just 'debug'.
    //   If you add flavors like lite, prod, etc. you'll have to list your debuggableVariants.
    // debuggableVariants = ["liteDebug", "prodDebug"]
    /* Bundling */
    //   A list containing the node command and its flags. Default is just 'node'.
    // nodeExecutableAndArgs = ["node"]
    //
    //   The command to run when bundling. By default is 'bundle'
    // bundleCommand = "ram-bundle"
    //
    //   The path to the CLI configuration file. Default is empty.
    // bundleConfig = file(../rn-cli.config.js)
    //
    //   The name of the generated asset file containing your JS bundle
    // bundleAssetName = "MyApplication.android.bundle"
    //
    //   The entry file for bundle generation. Default is 'index.android.js' or 'index.js'
    // entryFile = file("../js/MyApplication.android.js")
    //
    //   A list of extra flags to pass to the 'bundle' commands.
    //   See https://github.com/react-native-community/cli/blob/main/docs/commands.md#bundle
    // extraPackagerArgs = []
    /* Hermes Commands */
    //   The hermes compiler command to run. By default it is 'hermesc'
    // hermesCommand = "$rootDir/my-custom-hermesc/bin/hermesc"
    hermesCommand = "../node_modules/react-native/sdks/hermesc/%OS-BIN%/hermesc"
    //
    //   The list of flags to pass to the Hermes compiler. By default is "-O", "-output-source-map"
    // hermesFlags = ["-O", "-output-source-map"]
    /* Autolinking */
    autolinkLibrariesWithApp()
}

project.ext.vectoricons = [
    iconFontNames: [
        'FontAwesome.ttf',
        'FontAwesome5_Brands.ttf',
        'FontAwesome5_Regular.ttf',
        'FontAwesome5_Solid.ttf',
        'MaterialIcons.ttf',
        'MaterialCommunityIcons.ttf'
    ]
]
apply from: "../../../node_modules/react-native-vector-icons/fonts.gradle"

/**
 * Set this to true to create two separate APKs instead of one:
 *   - An APK that only works on ARM devices
 *   - An APK that only works on x86 devices
 * The advantage is the size of the APK is reduced by about 4MB.
 * Upload all the APKs to the Play Store and people will download
 * the correct one based on the CPU architecture of their device.
 */
def enableSeparateBuildPerCPUArchitecture = false

/**
 * Set this to true to Run Proguard on Release builds to minify the Java bytecode.
 */
def enableProguardInReleaseBuilds = false

/**
 * The preferred build flavor of JavaScriptCore (JSC)
 *
 * For example, to use the international variant, you can use:
 * def jscFlavor = io.github.react-native-community:jsc-android-intl:2026004.+`
 *
 * The international variant includes ICU i18n library and necessary data
 * allowing to use e.g. `Date.toLocaleString` and `String.localeCompare` that
 * give correct results when using with locales other than en-US. Note that
 * this variant is about 6MiB larger per architecture than default.
 */
def jscFlavor = 'io.github.react-native-community:jsc-android:2026004.+'

/**
 * Load the keystore + credentials for release build signing configuration
 */
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    println("Warning: keystore.properties file not found. Ensure this file is present for release build signing.")
}

android {
    ndkVersion rootProject.ext.ndkVersion
    lintOptions {
        // To ignore specific warnings/errors
        disable 'UnspecifiedRegisterReceiverFlag' // Disables the specific lint check
    }

    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdkVersion rootProject.ext.compileSdkVersion

    namespace "com.fedi"
    defaultConfig {
        applicationId "com.fedi"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 240051929
        versionName "26.2.4"
    }

     compileOptions {
        coreLibraryDesugaringEnabled true
    }

    packagingOptions {
        pickFirst 'lib/x86/libreact_nativemodule_core.so'
        pickFirst 'lib/x86_64/libreact_nativemodule_core.so'
        pickFirst 'lib/armeabi-v7a/libreact_nativemodule_core.so'
        pickFirst 'lib/arm64-v8a/libreact_nativemodule_core.so'
        pickFirst 'lib/x86/libturbomodulejsijni.so'
        pickFirst 'lib/x86_64/libturbomodulejsijni.so'
        pickFirst 'lib/armeabi-v7a/libturbomodulejsijni.so'
        pickFirst 'lib/arm64-v8a/libturbomodulejsijni.so'
        pickFirst 'lib/x86/libcrypto.so'
        pickFirst 'lib/x86_64/libcrypto.so'
        pickFirst 'lib/armeabi-v7a/libcrypto.so'
        pickFirst 'lib/arm64-v8a/libcrypto.so'
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            } else {
                storeFile file('debug.keystore')
                storePassword 'android'
                keyAlias 'androiddebugkey'
                keyPassword 'android'
            }
        }
    }
    buildTypes {
        debug {
            manifestPlaceholders = [
                networkSecurityConfig: "@xml/network_security_config_debug"
            ]
            signingConfig signingConfigs.debug
            packagingOptions {
                gradle.startParameter.getTaskNames().each { task ->
                    if (task.contains('assemble') || task.contains('bundle')) {
                        if (task.contains('nightly')) {
                            doNotStrip '**/libfediffi.so'
                        }
                    }
                }
            }
        }
        release {
            manifestPlaceholders = [
                networkSecurityConfig: "@xml/network_security_config_release"
            ]
            // Caution! In production, you need to generate your own keystore file.
            // see https://reactnative.dev/docs/signed-apk-android.
            signingConfig signingConfigs.release
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            proguardFile "${rootProject.projectDir}/../../node_modules/detox/android/detox/proguard-rules-app.pro"
            packagingOptions {
                gradle.startParameter.getTaskNames().each { task ->
                    if (task.contains('assemble') || task.contains('bundle')) {
                        if (task.contains('nightly')) {
                            doNotStrip '**/libfediffi.so'
                        }
                    }
                }
            }
        }
    }
    flavorDimensions "env"
    productFlavors {
        nightly {
            dimension "env"
            applicationIdSuffix ".nightly"
            // Force domain verification on install
            manifestPlaceholders = [autoVerify: "true"]
        }
        production {
            dimension "env"
            manifestPlaceholders = [autoVerify: "true"]
        }
    }
}

afterEvaluate {
    if (tasks.findByName('generateDebugLintReportModel')) {
        tasks.named('generateDebugLintReportModel').configure {
            dependsOn tasks.named('copyReactNativeVectorIconFonts')
        }
    }

    if (tasks.findByName('lintAnalyzeDebug')) {
        tasks.named('lintAnalyzeDebug').configure {
            dependsOn tasks.named('copyReactNativeVectorIconFonts')
        }
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
    // Configuration for Detox e2e testing framework
    androidTestImplementation('com.wix:detox:+')
    implementation 'androidx.appcompat:appcompat:1.1.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    implementation "org.fedi:fedi-android:0.1.2"

    // For animated GIF support
    implementation 'com.facebook.fresco:animated-gif:3.6.0'

    // For WebP support, including animated WebP
    implementation 'com.facebook.fresco:animated-webp:3.6.0'
    implementation 'com.facebook.fresco:webpsupport:3.6.0'

    // coroutines for async rpc
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1")

    // for push notifications from Firebase
    implementation platform('com.google.firebase:firebase-bom:33.7.0')
    implementation 'com.google.firebase:firebase-messaging'

    // Make sure to use the latest camera libraries for video recording
    // react-native-vision-camera uses 1.1.0 so we need to override here
    implementation "androidx.camera:camera-core:1.2.3"
    implementation "androidx.camera:camera-camera2:1.2.3"
    implementation "androidx.camera:camera-lifecycle:1.2.3"
    implementation "androidx.camera:camera-video:1.2.3"
    implementation "androidx.camera:camera-view:1.2.3"
    implementation "androidx.camera:camera-extensions:1.2.3"

    // The version of react-native is set by the React Native Gradle Plugin
    implementation("com.facebook.react:react-android")

    implementation project(':react-native-splash-screen')

    implementation "zendesk.messaging:messaging-android:$zendeskSdkVersion"


    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }
}

tasks.configureEach {
     if (name == 'generateReleaseLintVitalReportModel' ||
            name == 'generateDebugLintReportModel' ||
            name == 'generateNightlyReleaseLintReportModel' ||
            name == 'generateNightlyDebugLintReportModel' ||
            name == 'lintAnalyzeDebug' ||
            name == 'lintAnalyzeNightlyDebug') {
            dependsOn('copyReactNativeVectorIconFonts')
        }
}

//This is for Appium testing - to ensure the APK only installs to the selected device (rather than all devices)
tasks.withType(com.android.build.gradle.internal.tasks.InstallVariantTask).configureEach { task ->
    task.doLast {
        if (project.hasProperty('deviceId')) {
            def deviceId = project.getProperty('deviceId')
            def apkDir = task.apkDirectory.get().asFile
            def apkFiles = apkDir.listFiles().findAll { it.name.endsWith('.apk') }
            if (apkFiles.size() == 0) {
                throw new GradleException("APK not found in directory: ${apkDir}")
            }
            def apkFile = apkFiles[0]
            println "Installing ${apkFile} on device ${deviceId}"
            exec {
                executable 'adb'
                args '-s', deviceId, 'install', '-r', apkFile.absolutePath
            }
        }
    }
}
